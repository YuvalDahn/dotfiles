# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

pkgname=dmenu
pkgver=5.0
pkgrel=1
pkgdesc='Generic menu for X'
url='https://tools.suckless.org/dmenu/'
groups=('modified')
arch=('x86_64')
license=('MIT')
depends=('sh' 'glibc' 'coreutils' 'libx11' 'libxinerama' 'libxft' 'freetype2' 'fontconfig' 'libfontconfig.so')
source=(https://dl.suckless.org/tools/dmenu-${pkgver}.tar.gz
        https://tools.suckless.org/dmenu/patches/line-height/dmenu-lineheight-5.0.diff
        https://tools.suckless.org/dmenu/patches/highlight/dmenu-highlight-4.9.diff
        https://tools.suckless.org/dmenu/patches/mouse-support/dmenu-mousesupport-5.0.diff)
sha512sums=('2b6a7cdf5aefc5e7ca7a4944883c3c16ee6f5005d2a96b61482d4899ad395f9cb8926907681d88b9df3e1188cf421dad4cc17e343b752f6cb8b161d33384b3f3'
            '82c75d80e86fdc51b67f1d5dcc0ca4d26541c0d096e212e41e855cbec39361c5db1295cead54e5dd68a5dd0f0478f81855f1e3b2ec174601316b472dc1227bbe'
            '1db56b7c59e89e0925b2d6d2ce04db26cdf3dec32e80ad7c16c4afb92e733e83f5b8fd15eb5f3b77abb345decae1adb27d7a3662ad426d6e8edff5f98fa9e5e4'
            '14fa4282c9bb5a538ba3070c86b0f914df6013d4f0ab4d6eb6786a8b10158b1e1eb3d632deb4c54012e2040f7684131d658abd6c1ff27b2f4594da6db6f3291c')

prepare() {
  cd ${pkgname}-${pkgver}
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.diff ]] && patch --unified -i "../$src"
  done
  echo "CPPFLAGS+=${CPPFLAGS}" >> config.mk
  echo "CFLAGS+=${CFLAGS}" >> config.mk
  echo "LDFLAGS+=${LDFLAGS}" >> config.mk
}

build() {
  cd ${pkgname}-${pkgver}
  make \
	  X11INC=/usr/include/X11 \
	  X11LIB=/usr/lib/X11 \
	  FREETYPEINC=/usr/include/freetype2
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et nowrap:
