# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

pkgname=dmenu
pkgver=4.9
pkgrel=2
pkgdesc='Generic menu for X'
url='https://tools.suckless.org/dmenu/'
groups=('modified')
arch=('x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft' 'freetype2')
source=(https://dl.suckless.org/tools/dmenu-${pkgver}.tar.gz
        https://tools.suckless.org/dmenu/patches/line-height/dmenu-lineheight-4.9.diff
        https://tools.suckless.org/dmenu/patches/highlight/dmenu-highlight-4.9.diff
        https://tools.suckless.org/dmenu/patches/mouse-support/dmenu-mousesupport-4.9.diff)
sha512sums=('c2779209fe012de8ca1cdd72923da6d594f4a8368c85c3c0e0afd4ae489a95fe0e6f05a947d115b6b389aa7170ab14c2c645a2031353b0a08f38327ab461fe65'
            'b2fa2b33b9a2391285d0c3e5df22a9d68f4193bebb4720ce71678e8a87f43ae6c0a6b3f3fd3f63aa89be95880c04c8a83e43240b6ae4f90487bdb5a9325d084c'
            '1db56b7c59e89e0925b2d6d2ce04db26cdf3dec32e80ad7c16c4afb92e733e83f5b8fd15eb5f3b77abb345decae1adb27d7a3662ad426d6e8edff5f98fa9e5e4'
            'ecb58104a8c1b40e25fe19435850e657332e6112ef9afa121741052877d3249790a598404c1afc19b5b47ae5e419720de4443fa363db68cd68d1283ccf54d900')

prepare() {
  cd ${pkgname}-${pkgver}
  patch --unified -i ../dmenu-lineheight-4.9.diff
  patch --unified -i ../dmenu-highlight-4.9.diff
  patch --unified -i ../dmenu-mousesupport-4.9.diff
  echo "CPPFLAGS+=${CPPFLAGS}" >> config.mk
  echo "CFLAGS+=${CFLAGS}" >> config.mk
  echo "LDFLAGS+=${LDFLAGS}" >> config.mk
}

build() {
  cd ${pkgname}-${pkgver}
  make \
	  X11INC=/usr/include/X11 \
	  X11LIB=/usr/lib/X11 \
	  FREETYPEINC=/usr/include/freetype2
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et nowrap:
